# Problem 21

Starting with `C` code of the form

```C
long test(long x, long y) {
    long val = ???;
    if (???) {
        if (???){
            val = ???;
        } else {
            val = ???;
        }
    } else if (???) {
        val = ???;
    }
    return val;
}
```

`GCC` generates the following assembly code:

```asm
# long test(long x, long y)
# x in %rdi, y in %rsi
test:
    leaq    0(,%rdi,8), %rax
    testq   %rsi, %rsi
    jle     .L2
    movq    %rsi, %rax
    subq    %rdi, %rax
    movq    %rdi, %rdx
    andq    %rsi, %rdx
    cmpq    %rsi, %rdi
    cmovge  %rdx, %rax
    ret
.L2:
    addq    %rsi, %rdi
    cmpq    $-2, %rsi
    cmovle  %rdi, %rax
    ret
```

Fill in the missing expression in the `C` code.

## Answer

```C
long test(long x, long y) {
    long val = x * 8;
    if (y > 0) {
        if (x >= y){
            val = x & y;
        } else {
            val = y - x;
        }
    } else if (y <= -2) {
        val = x + y;
    }
    return val;
}
```

```asm
# long test(long x, long y)
# x in %rdi, y in %rsi
test:
    leaq    0(,%rdi,8), %rax    # %rax = x * 8
    testq   %rsi, %rsi          # y & y
    jle     .L2                 # jump if y <= 0 -> (SF ^ OF) | ZF
    movq    %rsi, %rax          # %rax = y
    subq    %rdi, %rax          # %rax = y - x
    movq    %rdi, %rdx          # %rdx = x
    andq    %rsi, %rdx          # %rdx = x & y
    cmpq    %rsi, %rdi          # x - y
    cmovge  %rdx, %rax          # if (x >= y), %rax = x & y
    ret                         # return
.L2:
    addq    %rsi, %rdi          # x = x + y
    cmpq    $-2, %rsi           # y - -2
    cmovle  %rdi, %rax          # if (y <= -2), %rax = x + y
    ret                         # return
```
